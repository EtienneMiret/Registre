import io.freefair.gradle.plugins.jsass.SassCompile

plugins {
  id 'io.freefair.jsass-base' version '1.0.0'
  id 'com.moowork.node' version '1.3.1'
}

apply plugin: 'war'
apply plugin: 'io.freefair.jsass-base'

description = 'Serveur Registre'
archivesBaseName = 'Registre'

dependencies {
  compile project(':Model')
  compile project(':Security')
  compile group: 'org.springframework', name: 'spring-context'
  compile group: 'org.springframework', name: 'spring-webmvc'
  compile group: 'org.springframework', name: 'spring-context-support'
  compile group: 'org.springframework', name: 'spring-orm'
  compile group: 'org.springframework.security', name: 'spring-security-config'
  compile group: 'org.springframework.security', name: 'spring-security-web'
  compile group: 'org.hibernate.validator', name: 'hibernate-validator'
  providedCompile group: 'org.slf4j', name: 'slf4j-api'
  providedCompile group: 'javax.servlet', name: 'javax.servlet-api'
  runtime group: 'org.thymeleaf', name: 'thymeleaf-spring5'
  runtime group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity5'
  testCompile group: 'junit', name: 'junit'
  testCompile group: 'org.mockito', name: 'mockito-core'
  testCompile group: 'org.assertj', name: 'assertj-core'
  testRuntime group: 'org.slf4j', name: 'slf4j-log4j12'
}

node {
  version = '11.13.0'
  yarnVersion = '1.15.2'
  download = true
  distBaseUrl = 'https://direct.nodejs.org/dist/'
}

task sass(type: SassCompile) {
  sourceDir file ('src/main/scss')
  destinationDir file("$buildDir/css")
  cssPath 'resources'
  sourceMapEnabled false
}

task tsc (type: YarnTask) {
  args = ['run', 'tsc', '--outdir', "$buildDir/js/resources"]

  inputs.file file ('tsconfig.json')
  inputs.dir file ('src/main/ts')
  outputs.dir file ("$buildDir/js")
  dependsOn yarn
}

war {
  from file (sass.destinationDir)
  from file ("$buildDir/js")
  webInf {
    from 'src/main/xhtml'
    into 'thymeleaf'
  }
  dependsOn sass
  dependsOn tsc
}

clean {
  delete 'node_modules'
}
